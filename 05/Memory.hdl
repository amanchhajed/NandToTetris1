// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // Put your code here:
    DMux(in=load, sel=address[14], a=loadRam, b=loadScreenKbd);
    RAM16K(in=in, load=loadRam, address=address[0..13], out=outRam);

    DMux(in=loadScreenKbd, sel=address[13], a=loadScreen, b=temp);
    Screen(in=in, load=loadScreen, address=address[0..12], out=outScreen);

    Keyboard(out=outKeyBoard);
    Or8Way(in=address[0..7], out=tempKbd1);
    Or8Way(in[0..4]=address[8..12], in[5..7]=false, out=tempKbd2);
    Or(a=tempKbd1, b=tempKbd2, out=tempKbd);
    Mux16(a=outKeyBoard, b=false, sel=tempKbd, out=kbdout);

    Mux16(a=outScreen, b=kbdout, sel=address[13], out=outScreenKeyboard);
    Mux16(a=outRam, b=outScreenKeyboard, sel=address[14], out=out);
}